WHITESPACE = _{ " " | "\r\n" | "\n" | "\t" }

line_comment = _{ "#" ~ (!NEWLINE ~ ANY)* }


block_comment = _{ "###" ~ (!"###" ~ ANY)* ~ "###" }

COMMENT = _{ block_comment | line_comment }

keywords = _{
    "fn" | 
    "obj" |
    "print" |
    "return" |
    "if" |
    "else" |
    "true" |
    "false" |
    "modify" |
    "const" |
    "self" | 
    "while" |
    "assert" |
    "class" |
    "constructor" |
    "Self"

}

integer = @{ '0'..'9' ~ ('0'..'9' | ("_" ~ ('0'..'9')))* }

hex_digit = _{ '0'..'9' | 'a'..'f' | 'A'..'F' }
hex_int = @{ "0x" ~ (hex_digit ~ (hex_digit | ("_" ~ (hex_digit)))*) }

byte = @{ "0b" ~ ('0'..'1' ~ ('0'..'1' | ("_" ~ ('0'..'1')))*) }
float = @{ (integer ~ "." ~ integer) | (integer ~ ("f" | "F")) }
bigint = @{ "B" ~ (hex_int | integer) }

number = { bigint | hex_int | byte | float | integer }
boolean = { "true" | "false" }

unary_minus = { "-" }
not = { "!" }

primitive = _{ boolean | number | string }

allow_bin_op = _{ primitive | ident | function }

math_primary = _{ allow_bin_op | "(" ~ math_expr ~ ")" }

math_prefix = _{ unary_minus | not }
math_postfix = _{ dot_chain | callable | list_index }

math_atom = _{ math_prefix? ~ math_primary ~ math_postfix? }
math_expr = { math_atom ~ (bin_op ~ math_atom)* }

bin_op = _{ add | subtract | multiply | divide | modulo | lte | gte | lt | gt | eq | neq | and | or | xor }
add = { "+" }
subtract = { "-" }
multiply = { "*" }
divide = { "/" }
modulo = { "%" }

lt = { "<" }
gt = { ">" }
lte = { "<=" }
gte = { ">=" }
eq = { "==" }
neq = { "!=" }

and = { "&&" }
or = { "||" }
xor = { "^" }

escape_character = _{ "\\" | "r" | "n" }
escape_sequence = @{ "\\" ~ escape_character }

string = @{ "\"" ~ (("\\\"") | (!("\"") ~ ANY))* ~ "\"" }


ident_chars = _{ (('a'..'z') | ('0'..'9') | ('A'..'Z') | "_") }

ident = @{ !('0'..'9') ~ ident_chars+ }
type = { function_type | list_type_open_only | list_type | ident }
ident_type = _{ ident ~ ":" ~ type }

function_arguments = { (value ~ ("," ~ value)*)? }
callable = { "(" ~ function_arguments ~ ")" }

block = { "{" ~ declaration* ~ "}" }

function_parameters = { ((ident ~ (":" ~ type)?))? ~ ("," ~ ((ident ~ (":" ~ type)?)))* }

function_type = { "fn" ~ "(" ~ (type ~ ("," ~ type)*)? ~ ")" ~ function_return_type?  }
// allow braces around the return type in case it is long
function_return_type = { "->" ~ (type | ("(" ~ type ~ ")")) }
function = { "fn" ~ "(" ~ (function_parameters) ~ ")" ~ function_return_type? ~ block }

list = { "[" ~ value? ~ ("," ~ value)* ~ "]" }
open_ended_type = { type ~ "..." }
list_type = { "[" ~ type? ~ ("," ~ (type ~ !("...")))* ~ ("," ~ open_ended_type)? ~ "]" }
list_type_open_only = { "[" ~ open_ended_type ~ "]" }
list_index = { ("[" ~ value ~ "]")+ }

value = { function | math_expr | ident | list }

assignment_flag = { "modify" | "const" }
// bad_assignment_flag = { &ident }
assignment_flags = { assignment_flag ~ assignment_flag* }
assignment_no_type = { ident ~ "=" ~ value }
assignment_type = { ident ~ ":" ~ type ~ "=" ~ value }
assignment_unpack = { "[" ~ (ident ~ ("," ~ ident)*) ~ "]" ~ "=" ~ value }

assignment_variant = _{ assignment_no_type | assignment_type | assignment_unpack }
assignment = { (assignment_flags ~ assignment_variant) | assignment_variant }

reassignment_primary = _{ ident | "(" ~ reassignment_expr ~ ")" }

dot_name_lookup = { ident }
dot_function_call = { ident ~ callable }
dot_chain_option = _{ dot_function_call | dot_name_lookup }
dot_chain = { ("." ~ dot_chain_option)+ }

reassignment_postfix = _{ list_index | dot_chain }
reassignment_expr = { reassignment_primary ~ reassignment_postfix }
reassignment = { reassignment_expr ~ "=" ~ value }

else_statement = { "else" ~ (block | if_statement) }

if_statement = { "if" ~ value ~ block ~ else_statement? }

while_loop = { "while" ~ value ~ block }

number_loop_inclusive = { "through" }
number_loop_exclusive = { "to" }

number_loop_bind_name = { "," ~ ident }

number_loop_step = { "step" ~ value }

number_loop = { "from" ~ value ~ (number_loop_inclusive | number_loop_exclusive) ~ (value) ~ (number_loop_step?) ~ (number_loop_bind_name?) ~ block }
continue_statement = { "continue" }
break_statement = { "break" }

print_statement = { "print " ~ value }
assertion = { "assert" ~  value }

return_statement = { "return " ~ value? }

class_variable = { assignment_flags? ~ ident ~ (":" ~ type)? }
class_constructor = { "constructor" ~ "(" ~ function_parameters ~ ")" ~ function_return_type? ~ block }
class_bound_function = { "fn" ~ ident ~ "(" ~ function_parameters ~ ")" ~ function_return_type? ~ block }

class_feature = _{ class_constructor | class_bound_function | class_variable }

class_body = { "{" ~ class_feature*  ~ "}" }

class = { "class" ~ ident ~ class_body }

declaration = { class | reassignment | assignment | print_statement | assertion | return_statement | continue_statement | break_statement | if_statement | while_loop | number_loop | math_expr }

file = {
    SOI ~
    (declaration | NEWLINE)* ~
    EOI
}